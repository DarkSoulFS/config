import com.diffplug.gradle.spotless.FormatExtension

plugins {
    id 'groovy-gradle-plugin'
    alias(libs.plugins.spotless)
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    if (!JavaVersion.current().java11Compatible) {
        toolchain.languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 11
}

dependencies {
    constraints {
        implementation(libs.build.japicmp) {
            because "Use a newer version than the japicmp gradle plugin provides"
        }
    }
    implementation gradleApi()
    api libs.build.indra
    api libs.build.indra.crossdoc
    api libs.build.spotless
    api libs.build.errorprone
    api libs.build.nullaway
    api libs.build.japicmp.plugin
    api libs.build.forbiddenApis
    api libs.build.goomph
}

spotless {
    def common = { FormatExtension conf ->
        conf.endWithNewline()
        conf.indentWithSpaces(4)
        conf.trimTrailingWhitespace()
        conf.toggleOffOn('@formatter:off', '@formatter:on')
    }
    ["groovy", "groovyGradle"].each {
        "$it"() { f ->
            common(f)
            f.importOrderFile(rootProject.file('../.spotless/configurate.importorder'))
            f.greclipse().configFile(
                rootProject.file('../.spotless/eclipse-java.xml'),
                rootProject.file('../.spotless/greclipse.properties')
            )
        }
    }
}
